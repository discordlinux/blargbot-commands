{//;load functions}
{inject;{get;_gitcmd.functions}}

{//;Function to output help}
{function;help;{set;!~todohelp;{embedbuild;
author.name:{guildname};
author.icon_url:{guildicon};
title:Help for todo;
color:{func.dlcolors;brightorange};
description:Create, manage, and view todo lists.;
fields.name:Flags;
fields.values:`-c`: Select the todo list category.

__**Arguments**__:;
fields.value:{prefix}todo add <item>;
fields.value:Add an `item` to a todo list.;
fields.inline:false;
fields.value:{prefix}todo edit <item number>;
fields.value:Edit `item number` in a todo list.;
fields.inline:false;
fields.value:{prefix}todo remove <item number>;
fields.value:Remove `item number` from a todo list.;
fields.inline:false;
}}
{//;only output json if commandname is help}
{if;{commandname};==;help;
{jget;~todohelp};
{embed;{jget;~todohelp}}
}
}

{//;Function to embed todo lists}
{function;embed;{fallback;[{uridecode;{get;~cat}}{if;{get;~cat};!=;Todo;{space}todo} list too long{semi} contents dumped to this link.]({dump;{for;~i;1;<;{length;{get;@todo.{get;~cat}.{userid}}};**{get;~i}**. {get;@todo.{get;~cat}.{userid};{get;~i}}{newline}}})}
{embed;{embedbuild;
author.name:{usernick};
author.icon_url:{useravatar};
title:{uridecode;{get;~cat}}{if;{get;~cat};!=;Todo;{space}Todo} List;
color:{func.usrclr;{userid}};
footer.text:{fallback;}{usernick}{if;{usernick};endswith;s;';'s} lists: {uridecode;{join;{get;@todo.{userid}.lists};,{space}}}{fallback};
description:{for;~i;1;<;{length;{get;@todo.{get;~cat}.{userid}}};**{get;~i}**. {get;@todo.{get;~cat}.{userid};{get;~i}}{newline}};}}

{//;check prefix}
{func.prefixtest}

{//;detect arguments}
{fallback;}}
{if;{flag;c};!=;;{set;~cat;{uriencode;{flag;c}}};{set;~cat;Todo}}
{if;{get;@todo.{get;~cat}.{userid}};==;;{set;@todo.{get;~cat}.{userid};["null"]}}
{if;{get;@todo.{userid}.lists};==;;
{set;!@todo.{userid}.lists;["{get;~cat}"]}}
{if;{get;@todo.{userid}.lists};includes;{get;~cat};{//;do nothing};
{push;@todo.{userid}.lists;{get;~cat}}}
{if;{flag;_};==;;{if;{length;{get;@todo.{get;~cat}.{userid}}};>;1;{func.embed};{throw;No items in {uridecode;{get;~cat}} list!}};
{fallback;}
{switch;{lower;{args;0}};
{//;Add an item}
["add","a"];
{if;{args;1};==;;{func.help};
{if;{length;{get;@todo.{get;~cat}.{userid}}};>=;21;{output;`Sorry, only 20 items allowed per category!`};
{if;{length;{join;{slice;{split;{flag;_};{space}};1};{space}}};>;396;{output;`Sorry, max characters allowed per item is 396!`};
{push;@todo.{get;~cat}.{userid};{join;{slice;{split;{flag;_};{space}};1};{space}}}
{if;{get;@todo.{userid}.lists};==;;
{set;!@todo.{userid}.lists;["{get;~cat}"]}}
{if;{get;@todo.{userid}.lists};includes;{get;~cat};{//;do nothing};
{push;@todo.{userid}.lists;{get;~cat}}}
{reactadd;{channelid};{messageid};<:SeemsGood:478441196755091461>}}}};
{//;Remove an item}
["remove","rm"];
{if;{args;1};==;;{func.help};
{if;{numformat;{args;1};};==;NaN;{func.help};
{if;{length;{get;@todo.{get;~cat}.{userid}}};<;{math;+;{args;1};1};{output;`Item not found in {uridecode;{get;~cat}} list!`};
{jset;@todo.{get;~cat}.{userid};{args;1};DeleteThisEntry!#%}
{set;!@todo.{get;~cat}.{userid};{filter;~item;{get;@todo.{get;~cat}.{userid}};{if;{get;~item};==;DeleteThisEntry!#%;false;true}}}
{if;{length;{get;@todo.{get;~cat}.{userid}}};<=;1;
{set;!@todo.{userid}.lists;{filter;~list;{get;@todo.{userid}.lists};{bool;{get;~list};!=;{get;~cat}}}}}
{reactadd;{channelid};{messageid};<:SeemsGood:478441196755091461>}}}};
{//;Edit an item}
["edit","ed"];
{if;{args;1};==;;{func.help};
{if;{join;{slice;{split;{flag;_};{space}};2};{space}};==;;{func.help};
{if;{numformat;{args;1};};==;NaN;{func.help};
{if;{length;{get;@todo.{get;~cat}.{userid}}};<;{math;+;{args;1};1};{output;`Item not found in {uridecode;{get;~cat}} list!`};
{if;{length;{join;{slice;{split;{flag;_};{space}};2};{space}}};>;396;{output;`Sorry, max characters allowed per item is 396!`};
{jset;@todo.{get;~cat}.{userid};{args;1};{join;{slice;{split;{flag;_};{space}};2};{space}}}
{set;@todo.{get;~cat}.{userid};{get;@todo.{get;~cat}.{userid}}}
{reactadd;{channelid};{messageid};<:SeemsGood:478441196755091461>}}}}}};
{//;Output help}
{func.help}}}
{fallback}