{inject;{get;_gitcmd.functions}}

{//;get input}
{set;!~count;{replace;{numformat;{flag;c};};NaN;1}}
{set;!~reason;{if;{flag;r};==;{null};No reason given;{flag;r}}}
{//;set modid based on commandname}
{switch;{commandname};
["warn","rule"];
{set;!~modid;{userid}};
{//;set modid to bot's id when ran outside of warn/rule}
{set;!~modid;{exec;botid}}
}

{//;check if message is reply}
{suppresslookup}
{switch;{commandname};
["warn","rule"];
{if;{flag;m};!=;{null};
{set;!~wmessageid;{flag;m}};
{set;!~wmessageid;{messagereply}}
}}

{//;get user to warn}
{if;{get;~wmessageid};!=;{null};
{set;!~userid;{messagesender;{get;~wmessageid}}};
{set;!~userid;{userid;{flag;_}}}
}

{//;check if valid user input}
{switch;{get;~userid};
["no input given","","{get;~modid}"];
{//;invalid user input}
{output;{embed;{embedbuild;
color:{func.dlcolors;red};
title:Warn Error;
description:Input does not contain valid user;
}}}
{return}
}

{//;warn user}
{set;!~warns;{warn;{get;~userid};{replace;{parseint;{get;~count}};NaN;};{get;~reason}}}

{//;check if warn succeeded}
{if;{parseint;{get;~warns}};==;NaN;
{//;warn failed}
{output;{embed;{embedbuild;
color:{func.dlcolors;red};
title:Warn Error;
description:Failed to warn {usermention;{get;~userid}}.  {get;~warns};
}}}
{return}
}

{//;delete message if messageid set}
{if;{get;~wmessageid};!=;{null};
{set;!~messagetext;{messagetext;{channelid};{get;~wmessageid}}}
{void;{delete;{channelid};{get;~wmessageid}}}
}

{//;Time out for 10 x number of warns if 3+ warnings}
{if;{get;~warns};>;2;
{void;{timeout;{get;~userid};{math;*;10;{get;~warns}}m;{get;~reason};noperms}}
}

{//;modlog}
{execcc;modlog;warn;{get;~userid};{userid};{get;~reason};{get;~count};{get;~messagetext}}
