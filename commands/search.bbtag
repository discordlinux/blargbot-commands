{//;load functions}
{inject;{get;_gitcmd.functions}}

{//;help function}
{function;help;{set;!~sehelp;{embedbuild;
author.name:{guildname};
author.icon_url:{guildicon};
title:Help for se;
color:{func.dlcolors;brightyellow};
description:Searches the web and returns results.

__**Arguments**__:;
fields.name:{prefix}se abs <query>;
fields.value:tldp Advanced Bash Scripting search for `query`.;
fields.inline:false;
fields.name:{prefix}se aw <query>;
fields.value:Arch Wiki earch for `query`.;
fields.inline:false;
fields.name:{prefix}se bbg <query>;
fields.value:tldp Bash Beginners Guide search for `query`.;
fields.inline:false;
fields.name:{prefix}se def <query>;
fields.value:Dictionary search for `query`.;
fields.inline:false;
fields.name:{prefix}se dw <query>;
fields.value:Debian wiki search for `query`.;
fields.inline:false;
fields.name:{prefix}se fw <query>;
fields.value:Fedora wiki search for `query`.;
fields.inline:false;
fields.name:{prefix}se game <query>;
fields.value:MobyGames search for `query`.;
fields.inline:false;
fields.name:{prefix}se gw <query>;
fields.value:Gentoo wiki search for `query`.;
fields.inline:false;
fields.name:{prefix}se lsst <query>;
fields.value:Linux Shell Scripting Tutorial search for `query`.;
fields.inline:false;
fields.name:{prefix}se osd <query>;
fields.value:openSUSE docs search for `query`.;
fields.inline:false;
fields.name:{prefix}se osw <query>;
fields.value:openSUSE wiki search for `query`.;
fields.inline:false;
fields.name:{prefix}se steam <query>;
fields.value:Steam Play compatability search for `query`.;
fields.inline:false;
fields.name:{prefix}se uw <query>;
fields.value:Ubuntu wiki search for `query`.;
fields.inline:false;
fields.name:{prefix}se yt <query>;
fields.value:Youtube search for `query`.;
fields.inline:false;
}}
{//;only output json if commandname is help}
{if;{commandname};==;help;
{jget;~sehelp};
{embed;{jget;~sehelp}}
}
}

{//;function to search ddg}
{function;ddgse;
{//;set searchmsg color and message}
{if;{get;~ddgsecolor};==;{null};
{jset;~qjson;color;{func.dlcolors;blurple};c};
{jset;~qjson;color;{get;~ddgsecolor};c}
}
{jset;~qjson;message;Searching ddg for `{params}`;c}
{//;output message to let users know we're doing something}
{func.searchmsg;{get;~qjson}}
{//;get ddg results}

{set;!~rawresults;{jget;{base64decode;{jget;{request;https://api.duckduckgo.com/?q={uriencode;! {params}}&format=json&pretty=1&no_html=1&skip_disambig=1&no_redirect=1};body}};Redirect}}
{//;set results to null if includes api.duckduckgo}
{if;{get;~rawresults};startswith;https://api.duckduckgo;
{set;!~ddgresult;{null}};
{set;!~ddgresult;{get;~rawresults}}
}

{//;check results}
{if;{get;~ddgresult};==;{null};
{//;no results}
{//;output no results and return}
{edit;{channelid};{get;~searchmsg};_delete;{embedbuild;
author.name:Result via ddg;
author.icon_url:https://cdn.discordapp.com/emojis/496122815578374147.png;
author.url:https://duckduckgo.com/?q={uriencode;{params}};
title:No results for '{params}'.;
url:https://duckduckgo.com/?q={uriencode;{params}};
color:{func.dlcolors;red};
description:No results for `{params}`.
> https://duckduckgo.com/?q={uriencode;{params}};
}}
{return;false}
}


{//;output result}
{edit;{channelid};{get;~searchmsg};{get;~ddgresult};_delete}

}

{//;function to search urban dictionary}
{function;urbandictionary;
{//;set searchmsg color and message}
{jset;~ujson;color;1D2439;c}
{jset;~ujson;message;Searching Urban Dictionary for `{params}`;c}
{//;output message to let users know we're doing something}
{func.searchmsg;{get;~ujson}}
{//;get results}
{if;{flagset;r};{set;~entry;{math;-;{flag;r};1}};{set;~entry;0}}
{fallback;}
{set;!~request;{jget;{request;http://api.urbandictionary.com/v0/define?term={uriencode;{params}}};body.list.{get;~entry}}}
{fallback}
{//;output results}
{if;{get;~request};==;{null};
{edit;{channelid};{get;~searchmsg};_delete;{embedbuild;
title:No Urban Dictionary results for '{params}'.;
color:{func.dlcolors;red};
description:No results for '{params}'.
}};
{edit;{channelid};{get;~searchmsg};{embedbuild;
title:__**Urban Dictionary definition for '{jget;~request;word}'**__;
url:{jget;~request;permalink};
color:1D2439;
description:{regexreplace;{jget;~request;definition};/[\[\]]/g;};
fields.name:__**Example**__:;
fields.value:{regexreplace;{jget;~request;example};/[\[\]]/g;};
fields.inline:false;
fields.name:‚úç;
fields.value:{jget;~request;author};
fields.inline:true;
fields.name:üëç;
fields.value:{jget;~request;thumbs_up};
fields.inline:true;
fields.name:üëé;
fields.value:{jget;~request;thumbs_down};
fields.inline:true;
fields.name:üìÖ;
fields.value:{jget;~request;written_on};
fields.inline:true;
footer.text:Use '{prefix} se urban {params} -r <number>' to see more results.;
}}}}

{//;function to get definitions from datamuse}
{function;datamuse-dictionary;
{//;set searchmsg color and message}
{jset;~djson;color;{func.dlcolors;blurple};c}
{jset;~djson;message;Searching for definition of `{params}`;c}
{//;output message to let users know we're doing something}
{func.searchmsg;{get;~djson}}
{fallback;[]}
{//;get results}
{set;!~json;{jget;{request;https://api.datamuse.com/words?sp={uriencode;{params}}&max=1&md=d};body}}
{if;{length;{get;~json}};==;0;
{set;!~word;{params}}
{set;!~def;No definition found for `{get;~word}`.};
{set;!~word;{jget;~json;0.word}}
{set;!~def;{foreach;~d;{jget;~json;0.defs};‚Ä¢{space}{regexreplace;{get;~d};/\t/g;:{space}}{newline}}}}
{//;output results}
{edit;{channelid};{get;~searchmsg};{embedbuild;
author.name:Datamuse Dictionary;
author.url:https://datamuse.com;
title:Definition of {get;~word};
url:https://api.datamuse.com/words?sp={uriencode;{params}}&max=1&md=d;
color:{func.dlcolors;blurple};
description:{get;~def};
footer.text:Powered by Datamuse.com;}}
}

{//;function to search steam and get protondb results}
{function;proton;
{//;set searchmsg color and message}
{jset;~pjson;color;1B2838;c}
{jset;~pjson;message;Searching Steam for `{params}`;c}
{//;output message to let users know we're doing something}
{func.searchmsg;{get;~pjson}}
{//;get results}
{switch;{lower;{params;0}};
["appid"];{set;!~id;{params;1}};
{set;!~gamewiki;{request;https://www.pcgamingwiki.com/w/api.php?action=query&format=json&prop=categories%7Cextlinks%7Cinfo&list=&meta=&generator=search&redirects=1&converttitles=1&utf8=1&formatversion=2&clcategories=Category%3ALinux%7CCategory%3AWindows%7CCategory%3AOS%20X&elprotocol=https&elquery=steamdb.info&elexpandurl=1&inprop=url%7Cdisplaytitle&gsrsearch={uriencode;{params}}&gsrwhat=nearmatch}}
{fallback;}
{set;!~id;{jget;{split;{jget;~gamewiki;body.query.pages.0.extlinks.0.url};/};4}}}
{//;use steam store API if pcgamingwiki fails}
{if;{get;~id};==;{null};
{fallback;}
{set;!~id;{jget;{request;https://store.steampowered.com/api/storesearch/?term={uriencode;{params}}&l=english&cc=US};body.items.0.id}}
}
{if;{get;~id};==;{null};
{edit;{channelid};{get;~searchmsg};_delete;{embedbuild;
color:{func.dlcolors;red};
description:'{params}' not found.}};
{//;protondb}
{set;!~pjson;{request;https://www.protondb.com/api/v1/reports/summaries/{get;~id}.json}}
{//;steam API}
{set;!~steamjson;{request;http://store.steampowered.com/api/appdetails?appids={get;~id}&l=English&cc=us}}
{set;!~desc;{substring;{htmldecode;{jget;~steamjson;body.{get;~id}.data.short_description}};0;300}}
{set;!~platforms;[]}
{if;{jget;~steamjson;body.{get;~id}.data.platforms.windows};==;true;
{push;~platforms;Windows}
{commit}}
{if;{jget;~steamjson;body.{get;~id}.data.platforms.mac};==;true;
{push;~platforms;macOS}
{commit}}
{if;{jget;~steamjson;body.{get;~id}.data.platforms.linux};==;true;
{push;~platforms;Linux}
{commit}}
{set;!~platforms;{join;{get;~platforms};,{space}}}
{if;{get;~platforms};==;{null};
{set;!~platforms;Windows}}
{fallback;0}
{//;current players}
{set;!~cplayers;{jget;{request;https://api.steampowered.com/ISteamUserStats/GetNumberOfCurrentPlayers/v0001/?appid={get;~id}};body.response.player_count}}
{//;steam deck}
{set;!~deckjson;{jget;{request;https://www.protondb.com/proxy/steam/deck-verified?nAppID={get;~id}};body}}
{switch;{jget;~deckjson;results.resolved_category};
["3"];
{set;!~deckstatus;‚úÖ Verified};
["2"];
{set;!~deckstatus;‚ö†Ô∏è Playable};
["1"];
{set;!~deckstatus;‚ùå Unsupported};
{set;!~deckstatus;‚ùì Unknown}}
{set;!~deckitems;[]}
{foreach;~item;{jget;~deckjson;results.resolved_items};
{set;!~itemtype}
{set;!~itemtoken}
{switch;{jget;~item;display_type};
["4"];
{set;!~itemtype;}
{set;!~itemtoken;};
["3","1"];
{set;!~itemtype;‚ö†Ô∏è}
{set;!~itemtoken;{trim;{join;{map;~l;{split;{jget;{split;{jget;~item;loc_token};_};2};};{if;{regextest;{get;~l};/[A-Z]/};{space}{get;~l};{get;~l}}};}}};
["2"];
{set;!~itemtype;‚ùå}
{set;!~itemtoken;{trim;{join;{map;~l;{split;{jget;{split;{jget;~item;loc_token};_};2};};{if;{regextest;{get;~l};/[A-Z]/};{space}{get;~l};{get;~l}}};}}};
{set;!~itemtype;üóíÔ∏è}
{set;!~itemtoken;{trim;{join;{map;~l;{split;{jget;{split;{jget;~item;loc_token};_};2};};{if;{regextest;{get;~l};/[A-Z]/};{space}{get;~l};{get;~l}}};}}}
}
{push;~deckitems;{get;~itemtype}{space}{replace;{capitalize;{get;~itemtoken};true};team{space}o{space}s;teamOS}}
}
{//;genres}
{set;!~genres;[]}
{foreach;~genre;{jget;~steamjson;body.{get;~id}.data.genres};{push;~genres;{jget;~genre;description}}}
{fallback;}
{//;output results}
{edit;{channelid};{get;~searchmsg};_delete;{embedbuild;
title:Steam ¬∑ {jget;~steamjson;body.{get;~id}.data.name} ¬∑ AppID: {get;~id};
url:https://store.steampowered.com/app/{get;~id};
color:1B2838;
image.url:https://steamcdn-a.akamaihd.net/steam/apps/{get;~id}/header.jpg;
footer.text:{get;~desc}{if;{length;{get;~desc}};>=;300;{space}[...]};
description:**OSes**: {get;~platforms}
**Developer**: {join;{jget;~steamjson;body.{get;~id}.data.developers};,{space}}
**Publisher**: {join;{jget;~steamjson;body.{get;~id}.data.publishers};,{space}}
**Genre**: {join;{jget;~genres};,{space}}
**Price**: {jget;~steamjson;body.{get;~id}.data.price_overview.final_formatted} ({jget;~steamjson;body.{get;~id}.data.price_overview.discount_percent}% discount)
**Recommendations**: {jget;~steamjson;body.{get;~id}.data.recommendations.total}
**Current Players**: {get;~cplayers}
{zws}
**[Steam Deck Status](https://steamdeck.com/en/verified)**: {get;~deckstatus}{if;{length;{jget;~deckitems}};!=;0;{newline}{clean;{join;~deckitems;{newline}}}}
{zws}
**[ProtonDB Tier](https://www.protondb.com/app/{get;~id})**
{if;{jget;~pjson;body.confidence};==;;No ratings found.;**{capitalize;{jget;~pjson;body.tier}}** (ratings: {jget;~pjson;body.total}, score: {jget;~pjson;body.score}{if;{jget;~pjson;body.trendingTier};!=;{jget;~pjson;body.tier};,{space}trending towards: {jget;~pjson;body.trendingTier}})}
}}
}
}

{//;function to search youtube}
{function;youtube;
{//;set searchmsg color and message}
{jset;~yjson;color;{func.dlcolors;blurple};c}
{jset;~yjson;message;Searching YouTube for `{params}`;c}
{//;output message to let users know we're doing something}
{func.searchmsg;{get;~yjson}}
{//;get results}
{fallback;}
{set;!~youtube.out;}
{set;!~res;{jget;{func.getjson;https://vid.puffyan.us/api/v1/search?q={uriencode;{params}}&hl=en-US};0.videoId}}
{if;{get;~res};!=;{null};
{set;!~youtube.out;https://youtu.be/{get;~res}};
{set;!~instances;{slice;{func.getjson;https://api.invidious.io/instances.json?sort_by=users,health};0;10}}

{foreach;~instance;{get;~instances};{if;{get;~youtube.out};==;{null};
{set;!~res;{jget;{func.getjson;{jget;~instance;1.uri}/api/v1/search?q={uriencode;{params}}&hl=en-US};0.videoId}}
{if;{get;~res};!=;{null};{set;!~youtube.out;https://youtu.be/{get;~res}}}
}}}
{//;output results}
{edit;{channelid};{get;~searchmsg};{if;{get;~youtube.out};!=;{null};{get;~youtube.out};https://youtu.be/c_l6Hu4Y3gI};_delete}
}

{//;check if prefix used properly}
{func.prefixtest}

{//;detect arguments}
{switch;{lower;{args;0}};
["!aw","aw","archwiki"];{set;!~ddgsecolor;{func.distrocolors;arch}}{func.ddgse;site:wiki.archlinux.org {args;1;n}};
["dw","debianwiki"];{set;!~ddgsecolor;{func.distrocolors;debian}}{func.ddgse;site:wiki.debian.org {args;1;n}};
["fw","fedorawiki"];{set;!~ddgsecolor;{func.distrocolors;fedora}}{func.ddgse;site:fedoraproject.org/wiki {args;1;n}};
["game","games"];{set;!~ddgsecolor;1673C0}{func.ddgse;site:mobygames.com {args;1;n}};
["gw","gentoowiki"];{set;!~ddgsecolor;{func.distrocolors;gentoo}}{func.ddgse;site:wiki.gentoo.org {args;1;n}};
["osw","opensusewiki"];{set;!~ddgsecolor;{func.distrocolors;opensuse}}{func.ddgse;site:en.opensuse.org {args;1;n}};
["osd","opensusedocs"];{set;!~ddgsecolor;{func.distrocolors;opensuse}}{func.ddgse;site:doc.opensuse.org {args;1;n}};
["uw","ubuntuwiki"];{set;!~ddgsecolor;{func.distrocolors;ubuntu}}{func.ddgse;site:wiki.ubuntu.com {args;1;n}};
["urban","urbandictionary"];{func.urbandictionary;{args;1;n}};
["proton","steam","protondb"];{func.proton;{args;1;n}};
["def","define","dict","dictionary"];{func.datamuse-dictionary;{args;1;n}};
["youtube","yt"];{func.youtube;{args;1;n}};
["lsst"];{set;!~ddgsecolor;skyblue}{func.ddgse;site:bash.cyberciti.biz/guide/ {args;1;n}};
["abs"];{set;!~ddgsecolor;skyblue}{func.ddgse;site:tldp.org/LDP/abs {args;1;n}};
["bbg"];{set;!~ddgsecolor;skyblue}{func.ddgse;site:tldp.org/LDP/Bash-Beginners-Guide {args;1;n}};
["au","askubuntu"];{set;!~ddgsecolor;{func.distrocolors;ubuntu}}{func.ddgse;site:askubuntu.com {args;1;n}};
["wiki","wikipedia"];{set;!~ddgsecolor;{func.dlcolors;blurple}}{func.ddgse;site:wikipedia.org {args;1;n}};
["-h","help","--help"];{func.help};
{if;{args};==;;{func.help}{return}}
{func.ddgse;{args}}}

{//;TODO:
image search
jargon lexicon}
