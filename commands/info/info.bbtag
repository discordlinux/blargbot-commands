{//;load functions}
{inject;{get;_gitcmd.functions}}

{//;function to output main info list}
{function;infolist;
{set;!~manifest;{jget;{request;https://github.com/discordlinux/blargbot-commands/raw/main/commands/info/manifest.json};body}}
{//;setup static parts of embed}
{set;!~embed;[]}
{push;~embed;title:Info List}
{push;~embed;color:{func.dlcolors;yellow}}
{push;~embed;footer.text:Any arguments not listed will return Wikipedia search results.}
{//;get rest of embed from manifest}
{foreach;~cat;{jget;~manifest;info.categories};
{push;~embed;fields.name:• {jget;~cat;name}}
{push;~embed;fields.value:{repeat;`;3}{newline}{join;{jget;~cat;contents};,{space}}{newline}{repeat;`;3}}}
{//;output embed}
{embed;{apply;embedbuild;{get;~embed}}}
}

{//;function to output a search message while we wait for results}
{function;searchmsg;
{set;!~wcolor;{randchoose;["5865F2","57F287","FEE75C","EB459E"]}}
{set;!~searchmsg;{output;{embed;{embedbuild;
color:{get;~wcolor};
thumbnail.url:https://cdn.discordapp.com/emojis/478073442311077889.gif?size=20;
description:Getting info for `{params}`...;}}}}
}

{//;function to get json from website using motyar}
{function;getjson;{jget;{split;{jget;{request;http://motyar.info/webscrapemaster/try/?url={uriencode;{params}}};body};<input{space}type};0}}

{//;function to get matching query from wikipedia}
{function;wikiquery;{fallback;{flag;_}}{lower;{jget;{func.getjson;https://api.datamuse.com/sug?v=enwiki&s={uriencode;{params}}&max=1};0.word}}{fallback}}

{//;function to check manifest for input}
{function;getinfo;
{//;output search message}
{func.searchmsg;{params}}
{//;get manifiest}
{set;!~manifest;{jget;{request;https://github.com/discordlinux/blargbot-commands/raw/main/commands/info/manifest.json};body.info.commands}}
{//;find matches}
{set;!~match;{filter;~m;{get;~manifest};{bool;{lower;{params}};==;{lower;{jget;~m;name}}}}}
{//;check for matches}
{if;{length;{get;~match}};==;0;
{//;no matches, try sending input through wikiquery function and check again}
{set;!~wikiquery;{func.wikiquery;{params}}}
{set;!~match;{filter;~m;{get;~manifest};{bool;{get;~wikiquery};==;{lower;{jget;~m;name}}}}}
}
{if;{length;{get;~match}};==;0;
{//;no matches, send to wikipedia}
{func.wikipedia;{get;~wikiquery}};
{//;check matching value}
{if;{jget;~match;0.value};endswith;.json;
{//;embed matching value}
{edit;{channelid};{get;~searchmsg};_delete;{jget;{request;https://github.com/discordlinux/blargbot-commands/raw/main/commands/info{jget;~match;0.value}};body}};
{//;get wikipedia result for matching value}
{func.wikipedia;{jget;~match;0.value}}
}
}
}

{//;function to get metadata for wikipedia results}
{function;metadata;
{jset;~head;method;POST;c}
{jset;~head;headers.user-agent;blargbot - {guildname} - {commandname} - {userid};c}
{jget;{request;http://syretia.cf/metadata?u={uriencode;{params}};{get;~head}};body}
}

{//;function to get info via wikipedia}
{function;wikipedia;
{//;get results from wikipedia API}
{set;!~query;{params}}
{set;!~request;{request;https://wikipedia.org/w/api.php?action=query&format=json&prop=extracts%7Cinfo%7Cpageimages%7Clinks&generator=search&formatversion=2&exintro=1&explaintext=1&inprop=url&piprop=original&pilimit=1&pilicense=any&gsrsearch={uriencode;{get;~query}}&gsrlimit=1&gsrqiprofile=engine_autoselect&gsrwhat=nearmatch&redirects=1&pllimit=25}}
{fallback;}

{//;setup embed}
{set;!~url;{jget;~request;body.query.pages.0.fullurl}}
{//;check if url was found in request}
{if;{get;~url};==;{null};
{//;no results}
{set;!~title;No results found for '{get;~query}'}
{set;!~wcolor;indianred}
{set;!~img;https://fakeimg.pl/1x1/2A2D30/?retina=1&text=.&font=noto}
{set;!~url;https://wikipedia.org};
{//;get info from request results}
{set;!~title;{jget;~request;body.query.pages.0.title}}
{set;!~desc;{clean;{jget;~request;body.query.pages.0.extract}}}
{set;!~img;{jget;~request;body.query.pages.0.original.source}}
{set;!~desc;{regexreplace;{regexreplace;{substring;{get;~desc};0;1900};/\.\s?/g;.{space}};/"/g;“}}
{if;{get;~desc};endswith;refer to:;
{set;!~desc;{get;~desc}
{reverse;{replace;{reverse;{foreach;~wlink;{jget;~request;body.query.pages.0.links};{jget;~wlink;title},{space}}};,;}}}}
{if;{logic;||;
{bool;{get;~img};==;{null}};
{bool;{get;~img};endswith;.svg}};
{set;!~img;{jget;{filter;~i;{func.metadata;{get;~url}};{bool;{jget;~i;name};==;og:image}};0.content}}
}
{if;{get;~img};==;https://en.wikipedia.org/static/apple-touch/wikipedia.png;
{set;!~img;https://fakeimg.pl/1x1/2A2D30/?retina=1&text=.&font=noto}
}
{if;{get;~img};==;{null};
{set;!~img;https://fakeimg.pl/1x1/2A2D30/?retina=1&text=.&font=noto}
}
{if;{get;~img};==;error;
{set;!~img;https://fakeimg.pl/1x1/2A2D30/?retina=1&text=.&font=noto}
}
}

{//;edit search message with results}
{edit;{channelid};{get;~searchmsg};_delete;{embedbuild;
author.name:{get;~title};
author.icon_url:https://upload.wikimedia.org/wikipedia/commons/thumb/2/25/Info_icon-72a7cf.svg/1200px-Info_icon-72a7cf.svg.png;
author.url:{get;~url};
color:{get;~wcolor};
thumbnail.url:{get;~img};
description:{get;~desc}{if;{length;{get;~desc}};>=;1900;{space}[...]}
*{get;~url}*;}}
}

{//;supported distros help function}
{function;supported.help;{embed;{embedbuild;
color:{func.dlcolors;yellow};
title:Help for supported;
description:__**Usage**__: `info supported <distro>`
Searches the [Discord Linux Supported Distros sheet](https://docs.google.com/spreadsheets/d/124ayAVJ1QPqEYZvtk_0uT3n-8QByf8LR7A43ah8QvDA/edit#gid=0) (maintained by <@!481350122136010755>) and outputs a list of results.;}}
}

{//;function to get supported distros spreadsheet}
{function;supported.json;
{jset;~head;method;POST;c}
{jset;~head;headers.user-agent;blargbot guild: {guildid} | in: {flag;_};c}
{slice;{jget;{request;http://syretia.cf/sheets?key=124ayAVJ1QPqEYZvtk_0uT3n-8QByf8LR7A43ah8QvDA;{get;~head}};body.table.rows};2;}
}

{//;function to search list of supported distros}
{function;supported;
{//;output search message}
{func.searchmsg;supported {params}}
{//;search list of distros from google sheet maintained by Faith}
{set;!~res;{filter;~d;{func.supported.json;{params}};{bool;{lower;{get;~d}};includes;{lower;{params}}}}}
{//;start setting up embed}
{set;!~embed;[]}
{push;~embed;title:Is {capitalize;{params}} Supported in Discord Linux?;url:https://docs.google.com/spreadsheets/d/124ayAVJ1QPqEYZvtk_0uT3n-8QByf8LR7A43ah8QvDA/edit?usp=drivesdk;color:{func.dlcolors;blurple}}
{//;check for results}
{if;{length;{get;~res}};==;0;
{//;no results}
{push;~embed;description:No results found for `{params}`}
{push;~embed;color:{func.dlcolors;red}};
{//;finish embed}
{fallback;}
{foreach;~r;{get;~res};{push;~embed;fields.name:__{jget;~r;c.0.v}__;fields.inline:true;fields.value:**Supported**: {jget;~r;c.1.v}{newline}**Experience Level**: {jget;~r;c.2.v}{newline}**Recommended**: {jget;~r;c.3.v}{if;{jget;~r;c.4.v};!=;{null};{newline}**Notes**: {jget;~r;c.4.v}}}}
{commit}
{fallback}
}
{//;edit search msg with results}
{edit;{channelid};{get;~searchmsg};_delete;{apply;embedbuild;{get;~embed}}}
}

{//;output message if not ran in #bot-spam asking if user wants to run command in current channel}
{if;{guildid};==;96230004047740928;
{if;{userhasrole;457281742844330004;{userid};quiet};!=;true;
{if;{lower;{args;0}};!=;recommended;
{switch;{channelid};
["174317464337776640"];
{//;do nothing};
{set;!~ifchkmsg;{send;{channelid};{null};{embedbuild;
color:{func.dlcolors;yellow};
description:Are you sure you want to use the `info` command in this channel?  If you are using it to get information for yourself, you should run it in <#174317464337776640>.
Enter `y` to run the info command in this channel or `n` to cancel.;}}}
{set;!~ifwaitmsg;{waitmessage;{channelid};{userid};true;60}}
{set;!~ifwaitmsgtxt;{messagetext;{channelid};{get;~ifwaitmsg;1}}}
{void;{delete;{channelid};{get;~ifwaitmsg;1}}
{delete;{channelid};{get;~ifchkmsg}}}
{if;{lower;{get;~ifwaitmsgtxt}};!=;y;
{delete}
{return}
}
}
}
}
}

{//;check input}
{fallback;}
{switch;{lower;{args;0}};
["help","list",""];
{//;output info list}
{func.infolist};
["supported"];
{//;supported distros}
{if;{length;{argsarray}};==;1;
{func.supported.help};
{func.supported;{args;1;n}}
};
{//;get info for input}
{func.getinfo;{flag;_}}
}