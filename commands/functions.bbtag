{//;make sure variables are working properly before we do anything}
{set;!~varcheck;testdata}
{if;{get;~varcheck};!=;testdata;
{throw;Variable test failed. Something is wrong with blargbot.}
{return}}

{//;return if _killswitch variable is set to 'engage' in case bot goes nuts}
{if;{get;_killswitch};==;engage;
{return}}

{//;function to make requests using scrapingant}
{function;ant;
{if;{length;{paramsarray}};>=;2;
{set;!~usrhead;{params;1}}
}
{foreach;~header;{jkeys;~usrhead;headers};
{jset;~anthead;headers.Ant-{get;~header};{jget;~usrhead;headers.{get;~header}};c}
}
{if;{jget;~usrhead;method};==;{null};
{jset;~anthead;method;GET;c};
{jset;~anthead;method;{jget;~usrhead;method};c}
}
{if;{length;{paramsarray}};>=;3;
{set;!~antbody;{params;2;n}}
}
{request;https://api.scrapingant.com/v2/general?x-api-key={randchoose;{jget;@ant}}&browser={flagset;b}&url={uriencode;{params;0}};{get;~anthead};{get;~antbody}}
}

{//;function to get info for a channel from discord's API}
{function;chaninfo;{fallback;{channelid}}{jget;{request;https://discord.com/api/channels/{if;{params;0};==;{null};{channelid};{channelid;{params;0}}};{get;@bot.sh.get}};body}{fallback}}

{//;function for distro colors}
{function;distrocolors;
{switch;{lower;{params;0}};["debian"];CE0056;
["ubuntu"];E95420;
["xubuntu"];0044AA;
["ubuntu mate","ubuntu-mate","ubuntumate","mate"];87A556;
["kubuntu"];0077c4;
["kde neon","kde-neon","kdeneon","neon"];3DC08D;
["arch"];1793D1;
["gentoo"];7A5ADA;
["opensuse"];73ba25;
["elementary","elementaryos"];8a715e;
["red hat","red-hat","redhat","rhel"];cb2026;
["manjaro"];34be5b;
["mint"];8bd83c;
["void"];478061;
["slackware"];7f93b4;
["antergos"];6a7ab9;
["solus"];5294e2;
["fedora"];3d65b9;
5865F2}
}

{//;function for discord linux colors}
{function;dlcolors;
{switch;{lower;{params;0}};
["black"];23272a;
["red"];ed4245;
["green"];57f287;
["yellow"];FEE75C;
["orange"];d2691e;
["blue"];5865f2;
["blurple"];5865f2;
["fuchsia"];eb459e;
["pink"];eb459e;
["cyan"];57f2d1;
["gray"];5c6166;
["brightblack"];3d4145;
["brightred"];dd6f71;
["brightgreen"];8dfcb0;
["brightyellow"];EBDC87;
["brightorange"];e68b49;
["brightblue"];8b93e8;
["brightblurple"];8b93e8;
["brightfuchsia"];dd78ae;
["brightpink"];dd78ae;
["brightcyan"];9ce2d3;
["brightgray"];92969b;
{randchoose;["23272a","ed4245","57f287","FEE75C","d2691e","5865f2","5865f2","eb459e","eb459e","57f2d1","5c6166","3d4145","dd6f71","8dfcb0","EBDC87","e68b49","8b93e8","8b93e8","dd78ae","dd78ae","9ce2d3","92969b"]}
}
}

{//;function to output error messages in an embed}
{function;error;
{output;{embed;{embedbuild;
title:Error;
description:{params};
color:{func.dlcolors;red};
footer.text:{usernick} ({userid});
footer.icon_url:{useravatar};
timestamp:{time}}}}
}

{//;function to output a fake image}
{function;fakeimg;https://fakeimg.pl/{if;{params;1};!=;{null};{params;1};250}x{if;{params;2};!=;{null};{params;2};100}/2A2D30/?retina=1&text={uriencode;{params;0}}&font=noto}

{//;function to get json from website using motyar}
{function;getjson;{jset;~head;headers.x-api-key;{randchoose;{jget;@ant}};c}{jget;{request;https://api.scrapingant.com/v1/general?url={uriencode;{params;0}}&browser=false&return_text=false;{get;~head}};body.content}}

{//;function to turn HTML into markdown using pandoc}
{function;htmltomd;{set;!~syretia.body;{params;0}}{func.syretia;pandoc?t=markdown_strict&f=html}}

{//;function to upload images to imagekit}
{function;imagekit;{jget;{func.getjson;https://api.imgbb.com/1/upload?key={if;{guildid};==;96230004047740928;{get;@dl.imgbb.key};{get;@imgbb.key}}&name={uriencode;{userid}-{time;X}}&image={uriencode;{params}}};data.url}}

{//;function to get TTS audio from lazypy.ro}
{function;lazypy;
{jset;~head;method;POST;c}
{jset;~head;headers.content-type;application/x-www-form-urlencoded;c}
{set;!~bod;text={uriencode;{params}}&service=StreamElements&voice=Brian}
{jget;{request;https://lazypy.ro/tts/request_tts.php;{jget;~head};{get;~bod}};body.audio_url}
}

{//;function to output linux tux hacker gif}
{function;linuxhacker;https://cdn.discordapp.com/emojis/478073442311077889.gif?size=20}

{//;function to evaluate expressions using mathjs}
{function;mathjs;{jget;{request;http://api.mathjs.org/v4/?expr={uriencode;{params;0}}&precision={if;{get;~precision};==;{null};10;{get;~precision}}};body}}

{//;function to reformat metadata using from syretia.xyz}
{function;metadata;
{foreach;~mde;{func.syretia;metadata?u={uriencode;{params;0}}};
{if;{jget;~mde;name};!=;{null};
{jset;~mdout;meta.{jget;~mde;name};{jget;~mde;content};c}}}
{jstringify;~mdout}
}

{//;function to get metadata using syretia.xyz}
{function;metadata_sy;
{func.syretia;metadata?u={uriencode;{params;0}}}
}

{//;function to detect if correct prefix was used}
{function;prefixtest;
{switch;{guildid};
["96230004047740928","204061452707954688"];
{set;!~used.prefix;{jget;{regexmatch;{replace;{messagetext};./{space};};/^(\.\/|bk\s|sb\s|bak\s|sudo\s|su\s|<)/i};0}}
{if;{get;~used.prefix};==;{null};
{return}}}
}

{//;function to output a progress bar}
{function;progress;{set;!~pl;{replace;{parseint;{params;1}};NaN;30}}{realpad;{repeat;█;{roundup;{math;*;{math;/;{get;~pl};100};{parseint;{params;0}}}}};{get;~pl};░}}

{//;function to get raw response from website using motyar's try page}
{function;rawhtml;{set;!~raw.req;true}{func.req;{params;0}}}

{//;function to get userid of reply sender}
{function;replysender;{if;{messagesender;{messagereply}};!=;{userid};{messagesender;{messagereply}}}}

{//;function that replicates built-in request, but through syretia.xyz}
{function;req;
{//;head for request}
{if;{jget;{params;1};method};!=;{null};
  {set;!~nuhead;{jget;{params;1}}};
  {jset;~nuhead;method;GET;c}
}
{if;{jget;~nuhead;headers};==;{null};
  {jset;~nuhead;headers.user-agent;blargbot (blargbot.xyz);c}
}

{//;setup headers aray}
{set;!~headers;[]}
{foreach;~key;{jkeys;~nuhead;headers};
  {if;{lower;{get;~key}};!=;x-authorization;
    {push;~headers;{get;~key};{jget;~nuhead;headers.{get;~key}}}
  }
}
{jset;~nuhead;headers.x-headers;{uriencode;{jget;~headers}};c}

{//;request}
{if;{length;{paramsarray}};>;2;
  {set;!~nureq;{request;https://syretia.xyz/api/v1/req?url={uriencode;{params;0}};{jget;~nuhead};{params;2;n}}};
  {set;!~nureq;{request;https://syretia.xyz/api/v1/req?url={uriencode;{params;0}};{jget;~nuhead}}}
}

{//;check status}
{if;{jget;~nureq;status};!=;200;
  {jset;~nureq;body;{get;~nureq};c}
}
{jget;~nureq;body}
}

{//;function to interact with firebase}
{function;rtdb;
{clean;{switch;{lower;{params;0}};
["get"];{set;!~fout;{request;https://discord-fde27-default-rtdb.firebaseio.com/{params;1}.json}}{if;{jget;~fout;status};==;200;{jget;~fout;body};{get;~fout}};
["set"];{jset;~head;method;PUT;c}{set;!~fout;{request;https://discord-fde27-default-rtdb.firebaseio.com/{params;1}.json?auth={get;@rtdb};{get;~head};{get;~rtdb.data}}}{if;{jget;~fout;status};==;200;{jget;~fout;body};{get;~fout}};
["delete","del","remove","rm"];{jset;~head;method;DELETE;c}{set;!~fout;{request;https://discord-fde27-default-rtdb.firebaseio.com/{params;1}.json?auth={get;@rtdb};{get;~head}}}{jget;~fout;status}}}
}

{//;function to output a search message while we wait for results}
{function;searchmsg;
{set;!~secolor;{jget;{params};color}}
{set;!~semsg;{jget;{params};message}}
{set;!~searchmsg;{output;{embed;{embedbuild;
color:{get;~secolor};
thumbnail.url:https://cdn.discordapp.com/emojis/478073442311077889.gif?size=20;
description:{get;~semsg}...;}}}}
}

{//;function to make requests to syretia.xyz endpoints}
{function;syretia;{jset;~head;headers.content-type;application/json;c}{jset;~head;method;POST;c}{jset;~head;headers.user-agent;blargbot | guild: {guildid};c}{jget;{request;http://syretia.xyz/{params;0};{get;~head};{get;~syretia.body}};body}}

{//;function to convert time input to seconds}
{function;timeconv;{clean;{//;Convert time input to seconds}
{set;~time;{time;X}}
{//;Set an array to loop through}
{set;~array;{split;{params};{space}}}
{//;Loop through each item from the array}
{foreach;~input;~array;
{//;Multiply input with 604800 to get amount of seconds}
{if;{get;~input};endswith;w;
{set;~weeks;{math;*;604800;{replace;{get;~input};w;}}}
{set;~time;{math;+;{get;~time};{get;~weeks}}}}
{//;Multiply input with 86400 to get amount of seconds}
{if;{get;~input};endswith;d;
{set;~days;{math;*;86400;{replace;{get;~input};d;}}}
{set;~time;{math;+;{get;~time};{get;~days}}}}
{//;Multiply input with 3600 to get amount of seconds}
{if;{get;~input};endswith;h;
{set;~hours;{math;*;3600;{replace;{get;~input};h;}}}
{set;~time;{math;+;{get;~time};{get;~hours}}}}
{//;Multiply input with 60 to get amount of seconds}
{if;{get;~input};endswith;m;
{set;~mins;{math;*;60;{replace;{get;~input};m;}}}
{set;~time;{math;+;{get;~time};{get;~mins}}}}
{//;Add input directly to time variable}
{if;{get;~input};endswith;s;
{set;~time;{math;+;{get;~time};{replace;{get;~input};s;}}}}}
{set;~time;{time;DD-MMM-YYYY HH:mm:ss;{get;~time};X}}
{get;~time}}}

{//;function to convert time input to seconds with more word handling}
{function;timeinsecs;
{foreach;~i;{split;{regexreplace;{regexreplace;{regexreplace;{regexreplace;{regexreplace;{regexreplace;{regexreplace;{params};/\s?(seconds|second|s)\s?/gi;s{space}};/\s?(days|day|d)\s?/gi;d{space}};/\s?(Months|months|Month|month|M)\s?/g;M{space}};/\s?(weeks|week|w)\s?/gi;w{space}};/\s?(years|year|y)\s?/gi;y{space}};/\s?(hours|hour|h)\s?/gi;h{space}};/\s?(Minutes|minutes|Minute|minute|m)\s?/g;m{space}};{space}};
{switch;{regexreplace;{get;~i};/[0-9]+/g;};
["y"];{set;!~tot;{math;+;0{get;~tot};{math;*;31536000;0{regexreplace;{get;~i};/[a-z]/gi;}}}};
["M"];{set;!~tot;{math;+;0{get;~tot};{math;*;2628000;0{regexreplace;{get;~i};/[a-z]/gi;}}}};
["w"];{set;!~tot;{math;+;0{get;~tot};{math;*;604800;0{regexreplace;{get;~i};/[a-z]/gi;}}}};
["d"];{set;!~tot;{math;+;0{get;~tot};{math;*;86400;0{regexreplace;{get;~i};/[a-z]/gi;}}}};
["h"];{set;!~tot;{math;+;0{get;~tot};{math;*;3600;0{regexreplace;{get;~i};/[a-z]/gi;}}}};
["m"];{set;!~tot;{math;+;0{get;~tot};{math;*;60;0{regexreplace;{get;~i};/[a-z]/gi;}}}};
["s"];{set;!~tot;{math;+;0{get;~tot};0{regexreplace;{get;~i};/[a-z]/gi;}}};
{set;!~tot;{math;+;0{get;~tot};0}}}}{get;~tot}
}

{//;function to remove duplicates from an array}
{function;uniq;
{set;!~filter.ar;[]}{void;{filter;~ue;{if;{isarray;{params}};{params};{paramsarray}};{if;{indexof;{get;~filter.ar};{get;~ue}};==;-1;{push;~filter.ar;{get;~ue}}true;false}}}{jvalues;~filter.ar}}

{//;function to get user's color based on role color}
{function;usrclr;{set;!~usrclrrole;{jget;{filter;~usrclrf;{roles;{userid;{params;0};quiet};quiet};{bool;{rolecolor;{get;~usrclrf}};!=;000000}};0}}{if;{get;~usrclrrole};!=;;{rolecolor;{get;~usrclrrole};quiet};5865F2}}

{//;function to check if user is in guild}
{function;usercheck;{suppresslookup}{if;{params;0};==;;false;{switch;{userid;{params;0};quiet};{guildmembers};true;false}}}

{//;function to get matching query from wikipedia}
{function;wikiquery;{fallback;{params;0}}{lower;{jget;{request;https://api.datamuse.com/sug?v=enwiki&s={uriencode;{params}}&max=1};body.0.word}}{fallback}}
