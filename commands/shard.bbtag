{//;load functions}
{inject;{get;_gitcmd.functions}}

{//;function to output loading message}
{function;loadingmsg;
{set;!~loadmsgid;{output;{embed;{embedbuild;
color:{func.dlcolors;green};
thumbnail.url:{func.linuxhacker};
description:Fetching info about shards...;}}}}

}

{//;function to parse user input}
{function;parseinput;
{//;get list of shards}
{set;!~shards.json;{jget;{request;https://blargbot.xyz/shards/json};body}}
{//;get number of clusters by matching regex}
{set;!~clusters;{length;{jsonkeys;~shards.json}}}
{//;get number of shards per cluster by checking shardCount in 1st cluster}
{set;!~shards.per.cluster;{jget;~shards.json;0.shardCount}}
{//;get total number of shards}
{set;!~shards;{math;*;{get;~clusters};{get;~shards.per.cluster}}}

{//;get shard based on guildid if -g is used or if -a is not used}
{if;{logic;||;
{bool;{flagset;a};==;false};
{bool;{flagset;g};==;true}};
{if;{flag;g};==;{null};
{set;!~guild;{guildid}};
{if;{parseint;{flag;g}};==;NaN;
{set;!~guild;{guildid}};
{set;!~guild;{parseint;{flag;g}}}}}
{//;get shard guild is running on}
{set;!~shard;{floor;{math;%;{math;/;{get;~guild};4194304};{get;~shards}}}}
{//;get cluster shard is in}
{if;{get;~shard};<=;3;
{set;!~cluster;0};
{set;!~cluster;{floor;{math;/;{get;~shard};{get;~shards.per.cluster}}}}}
{set;!~embed.title;Shard {get;~shard} Status}
{set;!~embed.desc;Guild `{get;~guild}` is running on shard `{get;~shard}` which is on cluster `{get;~cluster}`.}

}

{//;get shard specified if -s is used and valid shard specified}
{if;{logic;&&;
{bool;{parseint;{flag;s}};!=;NaN};
{bool;{parseint;{flag;s}};<=;{get;~shards}}};
{set;!~shard;{parseint;{flag;s}}}
{//;get cluster shard is in}
{if;{get;~shard};<=;3;
{set;!~cluster;0};
{set;!~cluster;{floor;{math;/;{get;~shard};{get;~shards.per.cluster}}}}}
{set;!~embed.title;Shard {get;~shard} Status}
{set;!~embed.desc;Shard `{get;~shard}` is running on cluster `{get;~cluster}`.}
}

{//;get cluster specified if -c is used and valid cluster specified}
{if;{logic;&&;
{bool;{parseint;{flag;c}};!=;NaN};
{bool;{parseint;{flag;c}};<;{get;~clusters}}};
{set;!~cluster;{parseint;{flag;c}}}
{set;!~embed.title;Cluster}
{set;!~embed.desc;Cluster `{get;~cluster}` is running {get;~shards.per.cluster} shards.}
}

{//;set embed title and desc if -a used}
{//;set embed title and desc if -a used}
{if;{get;~cluster};==;{null};
{//;get shard guild is running on}
{set;!~shard;{floor;{math;%;{math;/;{guildid};4194304};{get;~shards}}}}
{//;get cluster shard is in}
{if;{get;~shard};<=;3;
{set;!~gcluster;0};
{set;!~gcluster;{floor;{math;/;{get;~shard};{get;~shards.per.cluster}}}}}
{set;!~embed.title;Blargbot Shards Status}
{//;get number of guilds}
{foreach;~gc;{jsonkeys;~shards.json};
{foreach;~gs;{jget;~shards.json;{get;~gc}.shards};
{set;!~guilds;{math;+;0{get;~guilds};{jget;~gs;guilds}}}
}}
{set;!~embed.desc;Blargbot is running on {get;~clusters} clusters with {get;~shards.per.cluster} shards in each cluster for a total of {get;~shards} shards and {numformat;{get;~guilds};;;,} guilds.{newline}Your guild is running on shard `{get;~shard}` which is on cluster `{get;~gcluster}`.}
}

}

{//;function to output relevent json based on parsed input}
{function;rawjson;
{if;{get;~cluster};!=;{null};
{jget;~shards.json;{get;~cluster}};
{get;~shards.json}}
{return;false}

}

{//;function to emojify status originally by RagingLink (278237925009784832)}
{function;status;
{switch;{params};
ready;âœ…;
disconnected;âŒ;
handshaking;ðŸ¤;
connecting ;âŒ›;
â“}

}

{//;function to output embed}
{function;shardembed;
{//;check if valid json returned}
{if;{jstringify;{get;~shards.json}};==;{lb}{rb};
{//;invalid json returned}
{edit;{channelid};{get;~loadmsgid};_delete;{embedbuild;
title:Error Getting Shard Info;
url:https://blargbot.xyz/shards;
color:{func.dlcolors;red};
description:Unable to get list of shards from https://blargbot.xyz/shards/json;}}
{return;false}}

{//;set embeds variable}
{set;!~embeds;[]}
{push;~embeds;title:{get;~embed.title};description:{get;~embed.desc};color:{func.dlcolors;green};url:https://blargbot.xyz/shards;footer.text:Updated;timestamp:{time}}

{//;output only down clusters if -d is used}
{if;{flagset;d};
{foreach;~c;{jsonkeys;~shards.json};
{//;use filter to get list of down shards}
{set;!~down.shards.{get;~c};{filter;~sh;{jget;~shards.json;{get;~c}.shards};{bool;{jget;~sh;status};!=;ready}}}
{//;push down shards to embed}
{if;{length;{get;~down.shards.{get;~c}}};>;0;
{push;~embeds;fields.name:Cluster {get;~c};fields.value:Started: <t:{time;X;{jget;~shards.json;{get;~c}.readyTime};x}:R>
RAM: {numformat;{math;/;{jget;~shards.json;{get;~c}.rss};1000000};0}MB
Guilds: {jget;~shards.json;{get;~c}.guilds}
Shards:
{repeat;`;3}
{clean;{foreach;~s;{get;~down.shards.{get;~c}};{jget;~s;id} {func.status;{jget;~s;status}} {jget;~s;latency}ms{newline}}}
{repeat;`;3};fields.inline:true}}}
{//;check if any down shards were found}
{if;{length;{get;~embeds}};==;6;
{//;no down shards}
{push;~embeds;title:Down Shards;description:All shards are currently ready!};
{//;down shard(s) found}
{push;~embeds;title:Down Shards;description:;color:{func.dlcolors;red}}};

{//;else output info for all clusters if no cluster set in parseinfo function}
{if;{get;~cluster};==;{null};
{foreach;~c;{jsonkeys;~shards.json};
{push;~embeds;fields.name:Cluster {get;~c};fields.value:Started: <t:{time;X;{jget;~shards.json;{get;~c}.readyTime};x}:R>
RAM: {numformat;{math;/;{jget;~shards.json;{get;~c}.rss};1000000};0}MB
Guilds: {jget;~shards.json;{get;~c}.guilds}
Shards:
{repeat;`;3}
{clean;{foreach;~s;{jget;~shards.json;{get;~c}.shards};{jget;~s;id} {func.status;{jget;~s;status}} {jget;~s;latency}ms{newline}}}
{repeat;`;3};fields.inline:true}};

{//;else output info for cluster set in parseinfo function}
{push;~embeds;fields.name:Cluster {get;~cluster};fields.value:Started: <t:{time;X;{jget;~shards.json;{get;~cluster}.readyTime};x}:R>
RAM: {numformat;{math;/;{jget;~shards.json;{get;~cluster}.rss};1000000};0}MB
Guilds: {jget;~shards.json;{get;~cluster}.guilds}
Shards:
{repeat;`;3}
{clean;{foreach;~s;{jget;~shards.json;{get;~cluster}.shards};{jget;~s;id} {func.status;{jget;~s;status}} {jget;~s;latency}ms{newline}}}
{repeat;`;3}}
}
}

{//;edit loading message to contain embed created above}
{edit;{channelid};{get;~loadmsgid};_delete;{apply;embedbuild;{get;~embeds}}}

}

{//;run functions to get info and output embed}
{if;{flagset;r};{//;don't output loading message if -r used};{func.loadingmsg}}

{func.parseinput}

{if;{flagset;r};{func.rawjson}}

{func.shardembed}