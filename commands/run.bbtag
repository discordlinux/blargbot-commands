{//;inject functions}
{inject;{get;_gitcmd.functions}}

{//;function to run code through paiza.io}
{function;paiza;
{//;POST code to api.paiza.io}
{jset;~paiza.head;method;POST;c}
{jset;~paiza.head;headers.content-type;application/json;c}
{set;!~paiza.post.json;{request;https://api.paiza.io/runners/create;{jget;~paiza.head};{jget;~input.json}}}
{//;set variables from response}
{set;!~paiza.id;{jget;~paiza.post.json;body.id}}
{set;!~paiza.status;{jget;~paiza.post.json;body.status}}
{//;throw error if status is not completed}
{switch;{get;~paiza.status};
["running"];
{//;wait for run to finish}
{sleep;5s};
["completed"];
{//;do nothing};
{throw;Error running input: {get;~paiza.post.json}}
{file;buffer:{base64encode;{jstringify;{jget;~input.json}}};result.txt}
{return;false}
}
{//;get results}
{set;!~paiza.json;{request;https://api.paiza.io/runners/get_details?id={get;~paiza.id}&api_key=guest}}
{if;{jget;~paiza.json;body.result};==;timeout;
{throw;Paiza.io timeout}
{return;false}
}
{set;!~paiza.result;{replace;{jget;~paiza.json;body.stdout};Main.sh: line 1:{space};}
{replace;{jget;~paiza.json;body.stderr};Main.sh: line 1:{space};}
Exit code: {fallback;1}{jget;~paiza.json;body.exit_code}{fallback} | Memory used: {numformat;{math;/;{jget;~paiza.json;body.memory};1024};;;,} KiB | Time: {jget;~paiza.json;body.time} | Result: {jget;~paiza.json;body.result}}
[Link to Result]({dump;{get;~paiza.result}}.txt)
{file;buffer:{base64encode;{get;~paiza.result}};result.txt}
}

{switch;{lower;{args}};
["","help"];
{set;!~runhelp;{embedbuild;
author.name:{guildname};
author.icon_url:{guildicon};
title:Help for run;
color:{func.dlcolors;brightyellow};
description:Executes code using [Paiza.io](https://paiza.io/en).

__**Arguments**__:;
fields.name:{prefix}run <language> <code>;
fields.value:Executes the given `code` using the given `language`.  `language` must be from the list below.;
fields.inline:false;
fields.name:{prefix}run \`\`\`<language>
<code>
\`\`\`;
fields.value:Executes the given `code` using the given `language` in code block format.  `language` must be from the list below.;
fields.inline:false;
fields.name:**Paiza.io Supported Languages:**;
fields.value:{repeat;`;3}
{join;{sort;{jget;{request;https://api.paiza.io/runners.json};body.apis.0.operations.0.parameters.1.allowableValues.values}};,{space}}
{repeat;`;3};
fields.inline:false;
fields.name:Setting stdin;
fields.value:Data can be passed to stdin by replying to a message in chat.;
fields.inline:false;
}}
{//;only output json if commandname is help}
{if;{commandname};==;help;
{jget;~runhelp};
{embed;{jget;~runhelp}}
};
{//;set default json}
{set;!~input.json;{j;{"source_code":"help","input":"","language":"","api_key":"guest","network":true,"longpoll":true,"longpoll_timeout":30}}}
{//;set language}
{if;{jget;{split;{jget;{split;{messagetext};run{space}};1};{newline}};0};startswith;{repeat;`;3};
{jset;~input.json;language;{replace;{jget;{split;{jget;{split;{messagetext};run{space}};1};{newline}};0};{repeat;`;3};}};
{jset;~input.json;language;{jget;{split;{join;{slice;{split;{messagetext};run{space}};1;};run{space}};{space}};0}}
}
{//;set input if message is reply}
{if;{messagereply};!=;{null};
{jset;~input.json;input;{messagetext;{messagereply}}}
}
{//;set source_code}
{if;{jget;{split;{jget;{split;{messagetext};run{space}};1};{newline}};0};startswith;{repeat;`;3};
{jset;~input.json;source_code;{join;{slice;{split;{jget;{split;{messagetext};run{space}};1};{newline}};1;-1};{newline}}};
{jset;~input.json;source_code;{join;{slice;{split;{join;{slice;{split;{messagetext};run{space}};1;};run{space}};{space}};1;};{space}}}
}

{//;run code}
{func.paiza}
}
