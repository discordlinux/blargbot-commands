{//;inject functions}
{inject;{get;_gitcmd.functions}}

{//;function to run code through paiza.io}
{function;paiza;
{//;POST code to api.paiza.io}
{set;!~paiza.post.json;{request;http://api.paiza.io/runners/create;{j;{"method":"POST","headers":{"Content-Type":"application/json"}}};{jget;~input.json}}}
{//;set variables from response}
{set;!~paiza.id;{jget;~paiza.post.json;body.id}}
{set;!~paiza.status;{jget;~paiza.post.json;body.status}}
{//;try using rextester if status is not completed}
{if;{get;~paiza.status};!=;completed;
{func.rextester};
{//;get results}
{set;!~paiza.json;{request;http://api.paiza.io/runners/get_details?id={get;~paiza.id}&api_key=guest}}
{set;!~paiza.result;{replace;{jget;~paiza.json;body.stdout};Main.sh: line 1:{space};}
{replace;{jget;~paiza.json;body.stderr};Main.sh: line 1:{space};}
Exit code: {fallback;1}{jget;~paiza.json;body.exit_code}{fallback} | Memory used: {numformat;{math;/;{jget;~paiza.json;body.memory};1024};;;,} KiB | Time: {jget;~paiza.json;body.time} | Result: {jget;~paiza.json;body.result}}
[Link to Result]({dump;{get;~paiza.result}}.txt)
{file;buffer:{base64encode;{get;~paiza.result}};result.txt}
}
}

{//;function to run code through rextester}
{function;rextester;
{//;set language}
{switch;{jget;~input.json;language};
["assembly"];
{set;!~run.lang;15};
["bash"];
{set;!~run.lang;38};
["c#"];
{set;!~run.lang;1};
["c++"];
{set;!~run.lang;7};
["c"];
{set;!~run.lang;6};
["clientside"];
{set;!~run.lang;36};
["clojure"];
{set;!~run.lang;47};
["lisp"];
{set;!~run.lang;18};
["d"];
{set;!~run.lang;30};
["elixir"];
{set;!~run.lang;41};
["erlang"];
{set;!~run.lang;40};
["f#"];
{set;!~run.lang;3};
["fortran"];
{set;!~run.lang;45};
["go"];
{set;!~run.lang;20};
["haskell"];
{set;!~run.lang;11};
["java"];
{set;!~run.lang;4};
["javascript"];
{set;!~run.lang;17};
["kotlin"];
{set;!~run.lang;43};
["lua"];
{set;!~run.lang;14};
["mysql"];
{set;!~run.lang;33};
["node.js"];
{set;!~run.lang;23};
["ocaml"];
{set;!~run.lang;42};
["octave"];
{set;!~run.lang;25};
["objective-c"];
{set;!~run.lang;10};
["oracle"];
{set;!~run.lang;35};
["pascal"];
{set;!~run.lang;9};
["perl"];
{set;!~run.lang;13};
["php"];
{set;!~run.lang;8};
["postgresql"];
{set;!~run.lang;34};
["prolog"];
{set;!~run.lang;19};
["python"];
{set;!~run.lang;5};
["python3"];
{set;!~run.lang;24};
["r"];
{set;!~run.lang;31};
["rust"];
{set;!~run.lang;46};
["ruby"];
{set;!~run.lang;12};
["scala"];
{set;!~run.lang;21};
["scheme"];
{set;!~run.lang;22};
["sql"];
{set;!~run.lang;16};
["swift"];
{set;!~run.lang;37};
["tcl"];
{set;!~run.lang;32};
["vb"];
{set;!~run.lang;2}
}

{//;set head and body}
{jset;~run.head;method;POST;c}
{jset;~run.head;headers.content-type;application/x-www-form-urlencoded{semi} charset=UTF-8;c}
{set;!~run.body;LanguageChoiceWrapper={get;~run.lang}&Program={regexreplace;{uriencode;{jget;~input.json;source_code}};/%20/g;+}&Input={regexreplace;{uriencode;{jget;~input.json;input}};/%20/g;+}}

{//;run request}
{set;!~rex.json;{jget;{func.ant;https://rextester.com/rundotnet/Run;{jget;~run.head};{get;~run.body}};body}}

{//;output result}
{if;{jget;~rex.json;Stats};==;{null};
{//;error, output raw result}
[Link to Error Output]({dump;{get;~rex.json}}.txt)
{file;buffer:{base64encode;{get;~rex.json}};error.txt};
{//;no error, format and output}
{set;!~rex.result;{jget;~rex.json;Result}{if;{jget;~rex.json;Warnings};!=;{null};{newline}{jget;~rex.json;Warnings}}{if;{jget;~rex.json;Errors};!=;{null};{newline}{jget;~rex.json;Errors}}

{jget;~rex.json;Stats}}
[Link to Result]({dump;{get;~rex.result}}.txt)
{file;buffer:{base64encode;{get;~rex.result}};result.txt}
}
}

{switch;{lower;{args}};
["","help"];
{embed;{embedbuild;
title:Help for run;
color:yellow;
description:__**Usage**__: `run <language> <code to execute>`
Executes code using [Rextester.com](https://rextester.com/) or [Piaza.io](https://paiza.io).;
fields.name:**Example:**;
fields.value:run bash echo test;
fields.name:**Output:**;
fields.value:test

Exit code: 0 | Memory used: 3,593.75 KiB | Time: 0.00 | Result: success;
fields.name:**Rextester.com Supported Languages:**;
fields.value:{repeat;`;3}
assembly, bash, c#, c++, c, clientside, clojure, lisp, d, elixir, erlang, f#, fortran, go, haskell, java, javascript, kotlin, lua, mysql, node.js, ocaml, octave, objective-c, oracle, pascal, perl, php, postgresql, prolog, python, python3, r, rust, ruby, scala, scheme, sql, swift, tcl, vb
{repeat;`;3};
fields.name:**Paiza.io Supported Languages:**;
fields.value:{repeat;`;3}
{join;{sort;{jget;{request;http://api.paiza.io/runners.json};body.apis.0.operations.0.parameters.1.allowableValues.values}};,{space}}
{repeat;`;3};
fields.name:**Setting stdin**;
fields.value:Data can be passed to stdin by replying to a message in chat.;
}};
{//;set default json}
{set;!~input.json;{j;{"source_code":"help","input":"","language":"","api_key":"guest","network":true,"longpoll":true,"longpoll_timeout":30}}}
{//;set language}
{jset;~input.json;language;{jget;{split;{join;{slice;{split;{messagetext};run{space}};1;};run{space}};{space}};0}}
{//;set input if message is reply}
{if;{messagereply};!=;{null};
{jset;~input.json;input;{messagetext;{messagereply}}}
}
{//;set source_code}
{jset;~input.json;source_code;{join;{slice;{split;{join;{slice;{split;{messagetext};run{space}};1;};run{space}};{space}};1;};{space}}}

{//;run code}
{func.paiza}
}
