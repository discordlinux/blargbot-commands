{//;load functions}
{inject;{get;_gitcmd.functions}}

{//;help function}
{function;pollhelp;{embed;{embedbuild;
author.name:Help for poll;
color:yellow;
description:Creates a poll for the given question and duration. If no duration is given, defaults to 12 hours. 

**Available flags**
{repeat;`;3}
-t    How long before the poll expires, formatted as '1d2h3m4s'.  Defaults to 12 hours.

-d    Description for the poll.

-c    Color to set the poll.  Defaults to user's color.

-e    Emojis to use for the poll.  Defaults to <:upvote:466042779273920514> <:downvote:466042848454770730>.

-C    Channel to send the poll to.  Defaults to current channel.
{repeat;`;3}}}
}

{//;function to run poll}
{function;pollrun;
{set;!~pollmsg;{send;{get;~pollchan};;{embedbuild;
author.name:{username}#{userdiscrim} ({userid});
author.icon_url:{useravatar};
title:{get;~question};
color:{get;~ecolor};
description:{get;~desc};
footer.text:Expires;
timestamp:{func.timeconv;{get;~timer}};}}}
{apply;reactadd;{get;~pollchan};{get;~pollmsg};{get;~reactions}}
{timer;
{set;!~results;{foreach;~react;{get;~reactions};{newline}{get;~react} - __**{math;-;{length;{reactlist;{get;~pollchan};{get;~pollmsg};{get;~react}}};1} votes**__
{foreach;~user;{reactlist;{get;~pollchan};{get;~pollmsg};{get;~react}};{if;{get;~user};!=;134133271750639616;- {usernick;{get;~user}}{newline}}}}}
{void;{send;{get;~pollchan};;{embedbuild;
author.name:{username}#{userdiscrim} ({userid});
author.icon_url:{useravatar};
title:Results For {get;~question};
color:{get;~ecolor};
description:{get;~desc}{newline}
{get;~results};
footer.text:Expired;
timestamp:{time};}}};{get;~timer}}
}

{//;detect arguments}
{fallback;}
{if;{flag;C};!=;;
{switch;{channelid{flag;C}};
{channels};{set;!~pollchan;{channelid{flag;C}}};
{set;!~pollchan;{channelid}}};
{set;!~pollchan;{channelid}}}
{if;{flag;e};!=;;
{set;!~reactions;[]}
{foreach;~emote;{split;{flag;e};{space}};
{push;~reactions;{get;~emote}}};
{set;!~reactions;[]}
{push;~reactions;<:upvote:466042779273920514>;<:downvote:466042848454770730>}}
{if;{flag;c};!=;;
{set;!~ecolor;{flag;c}};
{set;!~ecolor;{func.usrclr;{userid}}}}
{if;{flag;t};!=;;
{set;!~timer;{flag;t}};
{set;!~timer;12h}}
{if;{flag;d};!=;;
{set;!~desc;{flag;d}}}
{switch;{lower;{flag;_}};
["help","--help","-h",""];
{func.pollhelp};
{set;!~question;{flag;_}}
{func.pollrun}
}