{//;inject functions}
{inject;{get;_gitcmd.functions}}

{//;function to get help from blargbot's API}
{function;helpapi;
{set;!~req;{request;https://blargbot.xyz/api/commands/{get;~cmd}}}

{//;error}
{if;{jget;~req;status};!=;200;
{output;{embed;{embedbuild;
author.name:{guildname};
author.icon_url:{guildicon};
title:Help not found for {get;~cmd};
color:{func.dlcolors;red};
description: No help entry found for `{get;~cmd}`.;
}}}
}

{//;setup embed array}
{set;!~emb.ar;[]}
{set;!~fields;[]}
{push;~emb.ar;title:Help for {jget;~req;body.name}}
{push;~emb.ar;url:https://blargbot.xyz/commands#{jget;~req;body.name}}
{push;~emb.ar;color:{get;~color}}
{push;~emb.ar;author.name:{guildname}}
{push;~emb.ar;author.icon_url:{guildicon}}

{//;flags}
{if;{length;{jget;~req;body.flags}};>;0;
{push;~fields;fields.name:Flags}
{if;{logic;&&;
{bool;{length;{jget;~req;body.aliases}};==;0};
{bool;{jget;~req;body.description};==;{null}}};
{push;~fields;fields.value:{foreach;~flag;{jget;~req;body.flags};`-{jget;~flag;flag} / --{jget;~flag;word}`: {jget;~flag;description}{newline}}{zws}{newline}__**Arguments**__:};
{push;~fields;fields.value:{foreach;~flag;{jget;~req;body.flags};`-{jget;~flag;flag} / --{jget;~flag;word}`: {jget;~flag;description}{newline}}}
}
{push;~fields;fields.inline:false};
}

{//;aliases}
{if;{length;{jget;~req;body.aliases}};>;0;
{if;{jget;~req;body.description};!=;{null};
{push;~emb.ar;description:{jget;~req;body.description}}
}
{push;~fields;fields.name:Aliases}
{push;~fields;fields.value:{zws}`{join;{jget;~req;body.aliases};`{space}`}`{newline}{zws}{newline}__**Arguments**__:}
{push;~fields;fields.inline:false};
{if;{jget;~req;body.description};!=;{null};
{push;~emb.ar;description:{jget;~req;body.description}{newline}{zws}{newline}__**Arguments**__:}
}}

{//;add description if no aliases or flags}
{if;{logic;&&;
{bool;{length;{jget;~emb.ar}};==;5};
{bool;{length;{jget;~fields}};==;0}};
{push;~emb.ar;description:__**Arguments**__:}
}

{//;arguments}
{if;{length;{jget;~req;body.signatures}};>;0;
{foreach;~sig;{jget;~req;body.signatures};
{if;{jget;~sig;hidden};!=;true;
{//;parts of argument}
{set;!~panam;[]}
{set;!~pdesc;}
{foreach;~param;{jget;~sig;parameters};
{if;{jget;~param;hidden}{jget;~param;kind};==;literal;
{push;~panam;{jget;~param;name}}
{if;{length;{jget;~param;aliases}};>;0;
{set;!~pdesc;Aliases for {jget;~param;name}: `{join;{jget;~param;aliases};`,}`{newline}{jget;~sig;description}};
{if;{get;~pdesc};==;{null};
{set;!~pdesc;{jget;~sig;description}}
}
};
{if;{get;~pdesc};==;{null};
{set;!~pdesc;{jget;~sig;description}}
}
{if;{logic;||;
{bool;0{jget;~param;minLength};>;0};
{bool;{jget;~param;required};==;true}};
{push;~panam;<{jget;~param;name}>};
{push;~panam;[{jget;~param;name}]}
}
}
}
{if;{length;{jget;~panam}};==;0;
{push;~fields;fields.name:{prefix}{jget;~req;body.name}}
{push;~fields;fields.value:{zws}{jget;~sig;description}};
{push;~fields;fields.name:{prefix}{jget;~req;body.name} {join;~panam;{space}}}
{push;~fields;fields.value:{zws}{get;~pdesc}}
}
{push;~fields;fields.inline:false}
}
}
}

{//;output embed}
{output;{embed;{apply;embedbuild;{jget;~emb.ar};{jget;~fields}}}}
}

{//;function to list help entries}
{function;helplist;
{//;setup embed base}
{set;!~hfields;[]}
{push;~hfields;author.name:{guildname} Commands}
{push;~hfields;author.icon_url:{guildicon}}
{push;~hfields;color:{func.dlcolors;blurple}}
{push;~hfields;footer.text:See '{prefix} help <command>' for help with a specific command}

{//;loop through each category and add entries to field array}
{foreach;~key;{jkeys;~helpjson};
{//;set category name to role mention if mention is valid}
{fallback;{get;~key}}
{set;!~catname;**{rolemention;{get;~key}} commands**}
{//;push name and entries to fields array}
{push;~hfields;fields.name:{zws}}
{push;~hfields;fields.value:{get;~catname}{newline}{repeat;`;3}{newline}{regexreplace;{foreach;~entry;{jkeys;{jget;~helpjson;{get;~key}}};{replace;{get;~entry};cht_sh;cht.sh},{space}};/,\s?$/;}{newline}{repeat;`;3}}
{push;~hfields;fields.inline:false}
}

{//;build help list embed}
{output;{embed;{apply;embedbuild;{jget;~hfields}}}}
}

{//;fetch help.json file}
{set;!~helpjson;{jget;{request;{get;@help.json}};body}}

{if;{get;~helpjson};==;{null};
{output;{embed;{embedbuild;
author.name:{guildname};
author.icon_url:{guildicon};
title:Error Fetching help.json;
color:{func.dlcolors;red};
description: Please make sure to set the variable `@help.json` to the URL of your help.json file.;
}}}
{return}
}

{//;output list if no input}
{if;{flag;_};==;{null};
{func.helplist}
{return}
}

{//;get input}
{set;!~cmd;{replace;{lower;{flag;_}};cht.sh;cht_sh}}

{//;check help.json for matching command}
{//;fallback to null for easy error checking}
{fallback;}
{//;check for matching entry in each category}
{foreach;~key;{jkeys;~helpjson};
{if;{jget;~helpjson;{get;~key}.{get;~cmd}};!=;{null};
{//;matching entry found}
{set;!~helpcat;{get;~key}}
{set;!~helptype;{jget;~helpjson;{get;~key}.{get;~cmd}}}
}}
{//;reset fallback}
{fallback}

{//;check if match found}
{if;{get;~helpcat};==;{null};
{output;{embed;{embedbuild;
author.name:{guildname};
author.icon_url:{guildicon};
title:Help not found for {get;~cmd};
color:{func.dlcolors;red};
description:No help entry found for `{get;~cmd}`.;
}}}
{return}
}

{//;set embed color based on category and type}
{if;{get;~helptype};==;api;
{//;use hex color if type is api}
{switch;{get;~helpcat};
["tty"];
{set;!~color;{func.dlcolors;gray}};
["users"];
{set;!~color;{func.dlcolors;brightyellow}};
["sudoers"];
{set;!~color;{func.dlcolors;brightorange}};
["trusted"];
{set;!~color;{func.dlcolors;brightred}};
["systemd"];
{set;!~color;{func.dlcolors;pink}}
};
{//;use dec color if not api}
{switch;{get;~helpcat};
["tty"];
{set;!~color;{jget;{embedbuild;color:{func.dlcolors;gray};};color}};
["users"];
{set;!~color;{jget;{embedbuild;color:{func.dlcolors;brightyellow};};color}};
["sudoers"];
{set;!~color;{jget;{embedbuild;color:{func.dlcolors;brightorange};};color}};
["trusted"];
{set;!~color;{jget;{embedbuild;color:{func.dlcolors;brightred};};color}};
["systemd"];
{set;!~color;{jget;{embedbuild;color:{func.dlcolors;pink};};color}}
}
}

{//;run helpapi function if type is api}
{if;{get;~helptype};==;api;
{func.helpapi}
{return}
}

{//;otherwise get embed from command and change color}
{set;!~helpembed;{execcc;{get;~helptype};help}}
{//;make sure embed is json}
{if;{jget;~helpembed};==;{jget;{null}};
{output;{embed;{embedbuild;
author.name:{guildname};
author.icon_url:{guildicon};
title:Help embed not found for {get;~helptype};
color:{func.dlcolors;red};
description:Error getting help embed for `{get;~helptype}`.;
}}}
{return}
}
{//;set embed color}
{jset;~helpembed;color;{get;~color};c}
{//;output help embed}
{output;{embed;{jget;~helpembed}}}
