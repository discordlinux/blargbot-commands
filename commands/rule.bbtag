{//;inject functions}
{inject;{get;_gitcmd.functions}}

{//;output help}
{if;;{logic;||;
{bool;{commandname};==;help};
{bool;{flag;_};==;help}};
{set;!~askhelp;{embedbuild;
author.name:Help for rule;
author.icon_url:{guildicon};
color:{func.dlcolors;brightorange};
description:__**Arguments**__:;
fields.name:{prefix}rule <number> <user>;
fields.value:Shortcut for the `warn` command.  Gives `user` a warning with reason 'Rule #`number`'.;
}}
{//;only output json if commandname is help}
{if;{commandname};==;help;
{jget;~askhelp};
{embed;{jget;~askhelp}}
}
{return;false}
}

{fallback;}

{//;check if message is reply}
{if;{func.replysender};!=;{null};
{set;!~ruser;{func.replysender}}
{set;!~rmsg;{messagereply}};
{set;!~ruser;{args;1}}
{set;!~rmsg;{flag;m}}
}

{//;check for required arguments}
{if;{logic;||;
{bool;{get;~ruser};==;{null}};
{bool;{args;0};==;{null}}};
{func.error;Missing required arguments.}
{return}
}

{//;warn user}
{execcc;warn;{get;~ruser};-c 1;-r Rule #{args;0};{if;{get;~rmsg};!=;{null};-m {get;~rmsg}}}
